plugins {
    id 'java'
    id 'jacoco'

    // https://plugins.gradle.org/plugin/org.openjfx.javafxplugin
    id 'org.openjfx.javafxplugin' version '0.0.12'
}

group 'de.uniks'
version '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'

    // https://mvnrepository.com/artifact/org.testfx/testfx-junit5
    testImplementation group: 'org.testfx', name: 'testfx-junit5', version: '4.0.16-alpha'

    // https://mvnrepository.com/artifact/org.testfx/openjfx-monocle
    testImplementation group: 'org.testfx', name: 'openjfx-monocle', version: 'jdk-12.0.1+2'

    testImplementation 'org.mockito:mockito-core:4.5.1'
    testImplementation 'org.mockito:mockito-junit-jupiter:4.5.1'

    implementation 'com.google.dagger:dagger:2.42'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.42'
    testAnnotationProcessor 'com.google.dagger:dagger-compiler:2.42'

    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-jackson:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava3:2.9.0'

    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.13.3'

    implementation group: 'javax.websocket', name: 'javax.websocket-api', version: '1.1'
    implementation group: 'javax.json', name: 'javax.json-api', version: '1.1.4'
    implementation group: 'org.glassfish', name: 'javax.json', version: '1.1.4'
    // This two dependencies must not be upgraded to version 2.x since it is incompatible with the javax websocket API
    implementation group: 'org.glassfish.tyrus', name: 'tyrus-client', version: '1.18'
    implementation group: 'org.glassfish.tyrus', name: 'tyrus-container-grizzly-client', version: '1.18'

    implementation "io.reactivex.rxjava3:rxjava:3.1.4"
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        attributes "Main-Class": "com.aviumauctores.pioneers.Main"
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

javafx {
    version = '17'
    modules = [ 'javafx.controls', 'javafx.graphics', 'javafx.fxml', 'javafx.media' ]
}

jacoco {
    toolVersion = '0.8.8'
}

jacocoTestReport {
    reports.xml.enabled = true
}

test {
    useJUnitPlatform()

    testLogging {
        exceptionFormat = 'full'
        showStackTraces = true
    }

    /*systemProperties = [
            'java.awt.headless': 'true',
            'testfx.robot'     : 'glass',
            'testfx.headless'  : 'true',
            'glass.platform'   : 'Monocle',
            'monocle.platform' : 'Headless',
            'prism.order'      : 'sw',
            'prism.text'       : 't2k',
    ]*/

    jvmArgs += [
            '-Xmx1g',
            '--add-opens', 'javafx.graphics/com.sun.glass.ui=ALL-UNNAMED',
            '--add-opens', 'javafx.graphics/com.sun.javafx.application=ALL-UNNAMED',
    ]
}